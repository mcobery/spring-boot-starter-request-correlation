plugins {
  id "com.github.hierynomus.license" version "0.16.1"
  id 'com.github.kt3k.coveralls' version '2.12.2'
  id "com.diffplug.spotless" version "6.21.0"
}

ext {
  versions = [
      'assertj'           : '3.24.2',
      'commons'           : [
          'lang3': '3.14.0'
      ],
      'feign'             : '12.4',
      'google'              : [
          'format'            : '1.19.1'
      ],      'junit'             : '4.13.2',
      'mockito'           : '5.13.0',
      'spring'            : [
          'boot'          : '3.1.5',
          'feign'         : '4.0.4'
      ],
  ]
}

apply plugin: 'java-library'
apply plugin: "jacoco"
apply plugin: 'idea'

apply from: 'license.gradle'
apply from: 'release.gradle'

apply plugin: 'pmd'

apply plugin: 'com.github.kt3k.coveralls'


apply from: "${rootProject.projectDir}/gradle/spotless.gradle"

// Release version that won't conflict with the bintray plugin
group = 'net.cobery'
version = '1.0.0'
archivesBaseName = "spring-boot-starter-request-correlation"

jar {
  manifest {
    attributes 'Implementation-Title': 'spring-boot-starter-request-correlation',
        'Implementation-Version': version
  }
}

repositories {
  jcenter()
}

configurations {
  archives
}

compileJava {
  options.fork = true
}

dependencies {
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${versions.spring.boot}"

  api "org.apache.commons:commons-lang3:${versions.commons.lang3}"

  // For Spring RestTemplate
  implementation "org.springframework.boot:spring-boot-starter-web:${versions.spring.boot}"
  // For Spring WebClient
  implementation "org.springframework.boot:spring-boot-starter-webflux:${versions.spring.boot}"

  // For Feign
  implementation "io.github.openfeign:feign-hc5:${versions.feign}"
  implementation "org.springframework.cloud:spring-cloud-starter-openfeign:${versions.spring.feign}"

  testImplementation "org.assertj:assertj-core:${versions.assertj}"
  testImplementation "org.mockito:mockito-core:${versions.mockito}"
  testImplementation "org.mockito:mockito-junit-jupiter:${versions.mockito}"
  testImplementation "org.springframework.boot:spring-boot-starter-test:${versions.spring.boot}"

}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter'
  }

  testLogging {
    events "skipped", "failed"
    exceptionFormat "full"
    afterSuite { desc, result ->
      if (!desc.parent) { // will match the outermost suite
        println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      }
    }
  }
}

